# [Loading Chemical Reaction Network Models from Files](@id model_file_import_export)
Catalyst stores chemical reaction network (CRN) models in `ReactionSystem` structures. This tutorial describes how to load such `ReactionSystem`s from, and save them to, files. This can be used to save models between Julia sessions, or transfer them from one session to another. Furthermore, to facilitate the computation modelling of CRNs, several standardised file formats have been created to represent CRN models (e.g. [SBML](https://sbml.org/)). This enables CRN models to be shared between different software and programming languages. While Catalyst itself does not have the functionality for loading such files, we will here (briefly) introduce a few packages that can load different file types to Catalyst `ReactionSystem`s.

## [Saving Catalyst models to, and loading them from, Julia files](@id model_file_import_export_crn_serialization)
Catalyst provides a `save_reactionsystem` function, enabling the user to save a `ReactionSystem` to a file. Here we demonstrate this by first creating a [simple cross-coupling model](@ref basic_CRN_library_cc):
```@example file_handling_1
using Catalyst
cc_system = @reaction_network begin
    k₁, S₁ + C --> S₁C
    k₂, S₁C + S₂ --> CP
    k₃, CP --> C + P
end
```
and next saving it to a file
```@example file_handling_1
save_reactionsystem("cross_coupling.jl", cc_system)
```
Here, `save_reactionsystem`'s first argument is the path to the file where we wish to save it. The second argument is the `ReactionSystem` we wish to save. To load the file, we use Julia's `include` function:
```@example file_handling_1
cc_loaded = include("cross_coupling.jl")
rm("cross_coupling.jl") # hide
cc_loaded # hide
```

!!! note
    The destination file can be in a folder. E.g. `save_reactionsystem("my\_folder/reaction_network.jl", rn)` saves the model to the file "reaction\_network.jl" in the folder "my_folder".

Here, `include` is used to execute the Julia code from any file. This means that `save_reactionsystem` actually saves the model as executable code which re-generates the exact model which was saved (this is the reason why we use the ".jl" extension for the saved file). Indeed, we can confirm this if we check what is printed in the file:
```
let

# Independent variable:
@parameters t

# Parameters:
ps = @parameters kB kD kP

# Species:
sps = @species S(t) E(t) SE(t) P(t)

# Reactions:
rxs = [
    Reaction(kB, [S, E], [SE], [1, 1], [1]),
    Reaction(kD, [SE], [S, E], [1], [1, 1]),
    Reaction(kP, [SE], [P, E], [1], [1, 1])
]

# Declares ReactionSystem model:
rs = ReactionSystem(rxs, t, sps, ps; name = Symbol("##ReactionSystem#12592"))
complete(rs)

end
```
!!! note
    The code that `save_reactionsystem` prints uses [programmatic modelling](@ref programmatic_CRN_construction) to generate the written model.

In addition to transferring models between Julia sessions, the `save_reactionsystem` function can also be used or print a model to a text file where you can easily inspect its components.

## [Loading and saving arbitrary Julia variables using Serialization.jl](@id model_file_import_export_julia_serialisation)
Julia provides a general and lightweight interface for loading and saving Julia structures to and from files that it can be good to be aware of. It is called [Serialization.jl](https://docs.julialang.org/en/v1/stdlib/Serialization/) and provides two functions, `serialize` and `deserialize`. The first allows us to write a Julia structure to a file. E.g. if we wish to save a parameter set associated with our model, we can use
```@example file_handling_2
using Serialization
ps = [:k₁ => 1.0, :k₂ => 0.1, :k₃ => 2.0]
serialize("saved_parameters.jls", ps)
```
Here, we use the extension ".jls" (standing for **J**u**L**ia **S**erialization), however, any extension code can be used. To load a structure, we can then use
```@example file_handling_2
loaded_sol = deserialize("saved_parameters.jls")
rm("saved_parameters.jls") # hide
loaded_sol # hide
```

## [Loading .net files using ReactionNetworkImporters.jl](@id model_file_import_export_sbml_rni_net)
A general-purpose format for storing CRN models is so-called .net files. These can be generated by e.g. [BioNetGen](https://bionetgen.org/). The [ReactionNetworkImporters.jl](https://github.com/SciML/ReactionNetworkImporters.jl) package enables the loading of such files to Catalyst `ReactionSystem`. Here we load a [Repressilator](@ref basic_CRN_library_repressilator) model stored in the "repressilator.net" file:
```julia
using ReactionNetworkImporters
prn = loadrxnetwork(BNGNetwork(), "repressilator.net")
```
Here, .net files not only contain information regarding the reaction network itself, but also the numeric values (initial conditions and parameter values) required for simulating it. Hence, `loadrxnetwork` generates a `ParsedReactionNetwork` structure, containing all this information. You can access the model as `prn.rn`, the initial conditions as `prn.u0`, and the parameter values as `prn.p`. Furthermore, these initial conditions and parameter values are also made [*default* values](@ref dsl_advanced_options_default_vals) of the model.

A parsed reaction network's content can then be provided to various problem types for simulation. E.g. here we perform an ODE simulation of our repressilator model:
```julia
using Catalyst, OrdinaryDiffEqDefault, Plots
tspan = (0.0, 10000.0)
oprob = ODEProblem(prn.rn, Float64[], tspan, Float64[])
sol = solve(oprob)
plot(sol; idxs = [:mTetR, :mLacI, :mCI])
```
![Repressilator Simulation](../assets/repressilator_sim_ReactionNetworkImporters.svg)

Note that, as all initial conditions and parameters have default values, we can provide empty vectors for these into our `ODEProblem`.


!!! note
    It should be noted that .net files support a wide range of potential model features, not all of which are currently supported by ReactionNetworkImporters. Hence, there might be some .net files which `loadrxnetwork` will not be able to load.

A more detailed description of ReactionNetworkImporter's features can be found in its [documentation](https://docs.sciml.ai/ReactionNetworkImporters/stable/).

## [Loading SBML files using SBMLImporter.jl and SBMLToolkit.jl](@id model_file_import_export_sbml)
The Systems Biology Markup Language (SBML) is the most widespread format for representing CRN models. Currently, there exist two different Julia packages, [SBMLImporter.jl](https://github.com/sebapersson/SBMLImporter.jl) and [SBMLToolkit.jl](https://github.com/SciML/SBMLToolkit.jl), that are able to load SBML files to Catalyst `ReactionSystem` structures. SBML is able to represent a *very* wide range of model features, with both packages supporting most features. However, there exist SBML files (typically containing obscure model features such as events with time delays) that currently cannot be loaded into Catalyst models.

SBMLImporter's `load_SBML` function can be used to load SBML files. Here, we load a [Brusselator](@ref basic_CRN_library_brusselator) model stored in the "brusselator.xml" file:
```julia
using SBMLImporter
prn, cbs = load_SBML("brusselator.xml", massaction = true)
```
Here, while [ReactionNetworkImporters generates a `ParsedReactionSystem` only](@ref model_file_import_export_sbml_rni_net), SBMLImporter generates a `ParsedReactionSystem` (here stored in `prn`) and a [so-called `CallbackSet`](https://docs.sciml.ai/DiffEqDocs/stable/features/callback_functions/#CallbackSet) (here stored in `cbs`). While `prn` can be used to create various problems, when we simulate them, we must also supply `cbs`. E.g. to simulate our brusselator we use:
```julia
using Catalyst, OrdinaryDiffEqDefault, Plots
tspan = (0.0, 50.0)
oprob = ODEProblem(prn.rn, prn.u0, tspan, prn.p)
sol = solve(oprob; callback = cbs)
plot(sol)
```
![Brusselator Simulation](../assets/brusselator_sim_SBMLImporter.svg)

Note that, while ReactionNetworkImporters adds initial condition and species values as default to the imported model, SBMLImporter does not do this. These must hence be provided to the `ODEProblem` directly.

A more detailed description of SBMLImporter's features can be found in its [documentation](https://sebapersson.github.io/SBMLImporter.jl/stable/).

!!! note
    The `massaction = true` option informs the importer that the target model follows mass-action principles. When given, this enables SBMLImporter to make appropriate modifications to the model (which are important for e.g. jump simulation performance).

### [SBMLImporter and SBMLToolkit](@id model_file_import_export_package_alts)
Above, we described how to use SBMLImporter to import SBML files. Alternatively, SBMLToolkit can be used instead. It has a slightly different syntax, which is described in its [documentation](https://github.com/SciML/SBMLToolkit.jl), and does not support as wide a range of SBML features as SBMLImporter. A short comparison of the two packages can be found [here](https://github.com/sebapersson/SBMLImporter.jl?tab=readme-ov-file#differences-compared-to-sbmltoolkit). Generally, while they both perform well, we note that for *jump simulations* SBMLImporter is preferable (its way for internally representing reaction event enables more performant jump simulations).

## [Loading models from matrix representation using ReactionNetworkImporters.jl](@id model_file_import_export_matrix_representations)
While CRN models can be represented through various file formats, they can also be represented in various matrix forms. E.g. a CRN with $m$ species and $n$ reactions (and with constant rates) can be represented with either
- An $mxn$ substrate matrix (with each species's substrate stoichiometry in each reaction) and an $nxm$ product matrix (with each species's product stoichiometry in each reaction).

Or
- An $mxn$ complex stoichiometric matrix (...) and a $2mxn$ incidence matrix (...).

The advantage of these forms is that they offer a compact and very general way to represent a large class of CRNs. ReactionNetworkImporters have the functionality for converting matrices of these forms directly into Catalyst `ReactionSystem` models. Instructions on how to do this are available in [ReactionNetworkImporter's documentation](https://docs.sciml.ai/ReactionNetworkImporters/stable/#Loading-a-matrix-representation).


---
## [Citations](@id petab_citations)
If you use any of this functionality in your research, [in addition to Catalyst](@ref doc_index_citation), please cite the paper(s) corresponding to whichever package(s) you used:
```
@software{2022ReactionNetworkImporters,
  author       = {Isaacson, Samuel},
  title        = {{ReactionNetworkImporters.jl}},
  howpublished = {\url{https://github.com/SciML/ReactionNetworkImporters.jl}},
  year         = {2022}
}
```
```
@software{2024SBMLImporter,
  author       = {Persson, Sebastian},
  title        = {{SBMLImporter.jl}},
  howpublished = {\url{https://github.com/sebapersson/SBMLImporter.jl}},
  year         = {2024}
}
```
```
@article{LangJainRackauckas+2024,
    url = {https://doi.org/10.1515/jib-2024-0003},
    title = {SBMLToolkit.jl: a Julia package for importing SBML into the SciML ecosystem},
    title = {},
    author = {Paul F. Lang and Anand Jain and Christopher Rackauckas},
    pages = {20240003},
    journal = {Journal of Integrative Bioinformatics},
    doi = {doi:10.1515/jib-2024-0003},
    year = {2024},
    lastchecked = {2024-06-02}
}
```
